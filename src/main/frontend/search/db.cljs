(ns ^:no-doc frontend.search.db
  (:require [cljs-bean.core :as bean]
            [clojure.string :as string]
            [frontend.db :as db]
            [frontend.db.model :as model]
            [frontend.handler.db-based.property.util :as db-pu]
            [frontend.state :as state]
            [frontend.config :as config]
            [frontend.util :as util]
            ["fuse.js" :as fuse]
            [datascript.impl.entity :as e]
            [frontend.handler.file-based.property.util :as property-util]))

;; Notice: When breaking changes happen, bump version in src/electron/electron/search.cljs

(defonce indices (atom nil))

(defn- max-len
  []
  (state/block-content-max-length (state/get-current-repo)))

(defn- sanitize
  [content]
  (some-> content
          (util/search-normalize (state/enable-search-remove-accents?))))

(defn- get-db-properties-str
  "Similar to db-pu/readable-properties but with a focus on making property values searchable"
  [properties]
  (->> properties
       (map
        (fn [[k v]]
          (let [values
                (->> (if (set? v) v #{v})
                     (map (fn [val]
                            (if (uuid? val)
                              (let [e (db/entity [:block/uuid val])
                                    value (or
                                           ;; closed value
                                           (db-pu/property-value-when-closed e)
                                           ;; page
                                           (:block/original-name e)
                                           ;; block generated by template
                                           (and
                                            (get-in e [:block/metadata :created-from-template])
                                            (:block/content e))
                                           ;; first child
                                           (let [parent-id (:db/id e)]
                                             (:block/content (model/get-by-parent-&-left (db/get-db) parent-id parent-id))))]
                                value)
                              val)))
                     (remove string/blank?))]
            (when (seq values)
              (str (:block/original-name (db/entity [:block/uuid k]))
                   ": "
                   (string/join "; " values))))))
       (remove nil?)
       (string/join ";; ")))

(defn block->index
  "Convert a block to the index for searching"
  [{:block/keys [uuid page content properties format]
    :or {format :markdown}
    :as block}]
  (let [repo (state/get-current-repo)]
    (when-not (> (count content) (max-len))
      (when-not (and (string/blank? content)
                     (empty? properties))
        (let [db-based? (config/db-based-graph? repo)
              content (if db-based? content
                          (property-util/remove-built-in-properties format content))
              m {:id (:db/id block)
                 :uuid (str uuid)
                 :page (if (or (map? page) (e/entity? page)) (:db/id page) page)
                 :content (sanitize content)}
              m' (cond-> m
                   (and db-based? (seq properties))
                   (update :content
                           (fn [content]
                             (str content "\n"
                                  (get-db-properties-str properties)))))]
          m')))))

(defn build-blocks-indice
  ;; TODO: Remove repo effects fns further up the call stack. db fns need standardization on taking connection
  #_:clj-kondo/ignore
  [repo]
  (->> (db/get-all-block-contents)
       (map block->index)
       (remove nil?)
       (bean/->js)))

(defn make-blocks-indice!
  ([repo] (make-blocks-indice! repo (build-blocks-indice repo)))
  ([repo blocks]
   (let [indice (fuse. blocks
                       (clj->js {:keys ["uuid" "content" "page"]
                                 :shouldSort true
                                 :tokenize true
                                 :minMatchCharLength 1
                                 :distance 1000
                                 :threshold 0.35}))]
     (swap! indices assoc-in [repo :blocks] indice)
     indice)))

(defn original-page-name->index
  [p]
  (when p
    {:name (util/search-normalize p (state/enable-search-remove-accents?))
     :original-name p}))

(defn make-pages-title-indice!
  "Build a page title indice from scratch.
   Incremental page title indice is implemented in frontend.search.sync-search-indice!
   Rename from the page indice since 10.25.2022, since this is only used for page title search.
   From now on, page indice is talking about page content search."
  []
  (when-let [repo (state/get-current-repo)]
    (let [pages (->> (db/get-pages (state/get-current-repo))
                     (remove string/blank?)
                     (map original-page-name->index)
                     (bean/->js))
          indice (fuse. pages
                        (clj->js {:keys ["name"]
                                  :shouldSort true
                                  :tokenize true
                                  :minMatchCharLength 1}))]
      (swap! indices assoc-in [repo :pages] indice)
      indice)))
